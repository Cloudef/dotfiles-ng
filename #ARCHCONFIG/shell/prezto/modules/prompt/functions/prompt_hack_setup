#!/bin/zsh
# Modified sorin's theme
#

# Load dependencies
pmodload 'helper'

function prompt_hack_precmd {
   setopt LOCAL_OPTIONS
   unsetopt XTRACE KSH_ARRAYS

   if (( $+functions[git-info] )); then
      git-info
   fi

   [[ ! -n "$PROMPT_TITLE" ]] && {
      _HCK_CUSTOM=""
   } || {
      _HCK_CUSTOM="%F{blue}$PROMPT_TITLE ";
   }

   [[ "$TERM" == "linux" ]] || [[ -n "$PROMPT_TITLE" ]] || \
      echo -ne "\033]0;${PWD}\007"

   PROMPT="%F{white}%1~%f${git_info:+${(e)git_info[prompt]}} ${_HCK_CUSTOM}%(!.%B%F{red}#%f%b.%B%F{green}]%f%b) "
}

function prompt_hack_setup {
   setopt LOCAL_OPTIONS
   unsetopt XTRACE KSH_ARRAYS
   prompt_opts=(cr percent subst)

   autoload -Uz add-zsh-hook
   add-zsh-hook precmd prompt_hack_precmd

   zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
   zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}F%f'
   zstyle ':prezto:module:editor:info:keymap:alternate' format ' %F{yellow}<%f%B%F{red}<%f%b%F{red}<%f '
   zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
   zstyle ':prezto:module:git:info:added' format ' %%B%F{green}+%f%%b'
   zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}A%f%%b'
   zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}B%f%%b'
   zstyle ':prezto:module:git:info:branch' format ':%F{red}%b%f'
   zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
   zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}X%f%%b'
   zstyle ':prezto:module:git:info:modified' format ' %%B%F{red}変%f%%b'
   zstyle ':prezto:module:git:info:position' format ':%F{green}%p%f'
   zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}R%f%%b'
   zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}*%f%%b'
   zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}合%f%%b'
   zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}僥%f%%b'
   zstyle ':prezto:module:git:info:keys' format \
      'prompt'  ' %F{cyan}git%f$(coalesce "%b" "%p" "%c")%s' \
      'rprompt' '%F{green}%A%B%S%a%d%m%r%U%u'

   RPROMPT='${editor_info[keymap]}${editor_info[overwrite]}%(?:: %f)${VIM:+" %B%F{green}V%f%b"}%F{green}[${git_info[rprompt]}%(?, %F{green}(⌐■_■), %F{yellow}%?%F{red}（╯°□°）╯ ┻━┻ )%f'
   SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_hack_setup "$@"

