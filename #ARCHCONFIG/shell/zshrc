#
# Systemwide zshrc
#

# source global shell file
# (tests for interactive shell)
source "/arch/shell/shellrc"

# execute in background
{
   # compile the completion dump to increase startup speed.
   dump_file="$HOME/.zcompdump"
   if [[ "$dump_file" -nt "${dump_file}.zwc" || ! -s "${dump_file}.zwc" ]]; then
      zcompile "$dump_file"
   fi
} &!

# prezto
export PREZTO="/arch/shell/prezto"

# temporary files
if [[ ! -d "$TMPDIR" ]]; then
   export TMPDIR="/tmp/$USER"
   mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"
if [[ ! -d "$TMPPREFIX" ]]; then
   mkdir -p "$TMPPREFIX"
fi

# other enviroiment crap
typeset -gU cdpath fpath mailpath manpath path
typeset -gUT INFOPATH infopath

# GNU info
infopath=(
   /usr/local/share/info
   /usr/share/info
   $infopath
)

# man
manpath=(
   /usr/local/share/man
   /usr/share/man
   $manpath
)

# add paths from /etc/manpaths.d as well
for path_file in /etc/manpaths.d/*(.N); do
   manpath+=($(<$path_file))
done
unset path_file

# ssh-agent forwarding
zstyle ':prezto:plugins:ssh' forwarding 'on'

# Set the key mapping style to 'emacs' or 'vi'.
zstyle ':prezto:module:editor' keymap 'vi'

# Auto convert .... to ../..
zstyle ':prezto:module:editor' dot-expansion 'no'

# Set case-sensitivity for completion, history lookup, etc.
zstyle ':prezto:*:*' case-sensitive 'no'

# Color output (auto set to 'no' on dumb terminals).
zstyle ':prezto:*:*' color 'yes'

# Use trapd00r's LS_COLORS, so disable prezto one
zstyle ':prezto:module:utility:ls' color 'no'

# don't show $PWD in ../
zstyle ':completion:*' ignore-parents 'pwd'

# ignore commands I don't have
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'

# Load zsh modules
zstyle ':prezto:load' 'zfunction' 'zargs' 'zmv'

# Syntax hilight
zstyle ':prezto:module:syntax-highlighting' highlighters \
   'main' \
   'brackets' \
   'pattern' \
   'root'

[[ -d "$ZDOTDIR/.zprezto" ]] && {

[[ "$(whoami)" == "root" ]] || [[ "$TERM" == "linux" ]] && {

# Set the prezto modules to load (browse modules).
# The order matters.
zstyle ':prezto:load' pmodule \
   'environment' \
   'terminal' \
   'editor' \
   'directory' \
   'archive' \
   'command-not-found' \
   'spectrum' \
   'utility' \
   'completion' \
   'ssh' \
   'git' \
   'prompt'

} || {

# Set the prezto modules to load (browse modules).
# The order matters.
zstyle ':prezto:load' pmodule \
   'environment' \
   'terminal' \
   'editor' \
   'history' \
   'directory' \
   'archive' \
   'command-not-found' \
   'spectrum' \
   'utility' \
   'completion' \
   'ssh' \
   'git' \
   'syntax-highlighting' \
   'prompt'

}

} # load if OK

# set prompt
zstyle ':prezto:module:prompt' theme 'hack'

# launch
source "$PREZTO/init.zsh"

[[ -d "$ZDOTDIR/.zprezto" ]] && {

# link completitions
compdef pacmatic=pacman

} # link if OK

# gets defined from utility module, but we don't want this
unfunction make

# load zcalc
autoload -U zcalc
alias zcalc='noglob zcalc'

# source functions and aliases
source "/arch/shell/functions"
source "/arch/shell/alias"

# zsh options
unsetopt correct # I can't see why people like this
                 # stop typing wrong silly people
bindkey '\e'      send-break           # Cancel completion with ESC
bindkey "\e[1~"   beginning-of-line    # Home
bindkey "^A"      beginning-of-line    # CTRL+A
bindkey "\e[4~"   end-of-line          # End
bindkey "\e[5~"   beginning-of-history # PageUp
bindkey "\e[6~"   end-of-history       # PageDown
bindkey "\e[2~"   quoted-insert        # Ins
bindkey "\e[3~"   delete-char          # Del
bindkey "\e[5C"   forward-word
bindkey "\eOc"    emacs-forward-word
bindkey "\e[5D"   backward-word
bindkey "\eOd"    emacs-backward-word
bindkey "\e\e[C"  forward-word
bindkey "\e\e[D"  backward-word
bindkey "\e[Z"    reverse-menu-complete # Shift+Tab
bindkey "\e[7~"   beginning-of-line     # Home
bindkey "\e[8~"   end-of-line           # End
bindkey "\eOH"    beginning-of-line
bindkey "\eOF"    end-of-line
bindkey "^I"      menu-complete         # Why press tab twice?
bindkey "^Z"      undo                  # Undo
bindkey '^R' history-incremental-search-backward

# Interactive sanity
[[ ! -n "$ZDOTDIR" ]] && {
   echo ""
   echo "* \$ZDOTDIR isnt exported"
   echo "* Export \$ZDOTDIR point to your directory that will"
   echo "* contain symlinked .zprezto directory for module loading"
   echo ""
} || {
   [[ ! -d "$ZDOTDIR/.zprezto" ]] && {
      echo ""
      echo "* You forgot to symlink \"$PREZTO\" to \"$ZDOTDIR/.zprezto\""
      echo ""
   }
}
